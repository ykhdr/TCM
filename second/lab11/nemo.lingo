prog = ws decl*:d stat:b { NmProg(:d, :b)};
decl = "VAR" ws id:v ":" ws type:t ";" ws { NmDecl(:v, :t) };

type  = int | array;
int   = "INT" ws { NmIntType() };
array = "[" ws type:t "]" ws { NmArrType(:t) };

stat   = test | seq | choice | loop | assign ;
test   = "(" ws expr:e1 cond$s ws expr:e2 ")" ws "?" ws { NmTest($s, :e1, :e2) };
seq    = ws "{" ws stat:s1 ";" ws stat:s2 seqs*:ss "}" ws { createSeq(:s1, :s2, :ss) };
seqs   = ";" ws stat;
choice = ws "{" ws stat:s1 "U" ws stat:s2 chs*:ss "}" ws { createChoice(:s1, :s2, :ss) };
chs    = ws "U" ws stat;
loop   = "{" stat:b ws "}" ws "*" ws { NmLoop(:b) };
assign = id:v ":=" ws expr:e ws { NmAssign(:v, :e) };

expr   = const | binOp | unOp | update | apply | var;
const  = digit+ $s ws { NmInt(s2i($s)) };
binOp  = "(" ws expr:l ("+"|"*"|"/"|"-"|"%") $s ws expr:r ")" ws { NmBinOp($s, :l, :r) };
unOp   = "-" $s ws expr:e { NmUnOp($s, :e) };
apply  = "APP" ws "(" ws expr:a "," ws expr:i ")" ws { NmApp(:a, :i) };
update = "UPD" ws "(" ws expr:a "," ws expr:i "," ws expr:v ")" ws { NmUpd(:a, :i, :v) };
arrval = "," ws expr;

var    = id:v { NmVar(:v) };
cond   = "!=" | "==" | "<=" | ">=" | "<" | ">" ;

id = letter+ $s ws { $s };
letter = 'a'-'z';
digit  = '0'-'9';
ws = (' ' | '\t' | '\n' | '\r')*;
